#!/bin/zsh
op=(-dmenu -markup-rows -format d -width -15 -theme grznych)
rt() {
    local u a sel=() i=0 m=(-l $((#/3)))
    for tit key _ in $@ ; do
        [[ $key[2] ]] && eval $key[2]+=$i,
        m+=(-kb-select-$((++i)) ${k::=$key[1]})
        [[ $tit:l != $k* && $tit =~ [$k$k:u] ]] &&
        tit=${tit/$MATCH/<u>$MATCH</u>} ; sel+=($tit)
    done
    i=`rofi $op $m ${u:+-u} $u ${a:+-a} $a <<< ${(F)sel}` && eval "$@[i*3] &"
}

    #title         #key #command
ap=(Terminal        t   urxvtc
    TeXstudio       x   texstudio
    Calculator      c   gnome-calculator
    Mathematica\ 11 mu  ~/.mathematica/Mathematica
    Other           oa  'rt $ot'
    Internet        ia  'rt $it')

ot=(Disks           da  gnome-disks
    GParted         ga  gparted
    Baobab          b   baobab
    Evince          e   evince
    Sims\ 4         iu  ~/Downloads/Sims_4_Linux/start.sh
    Steam           s   steam)

it=(Firefox         f   firefox\ --private-window
    Tor             t   tor-browser
    Telegram        g   telegram-desktop
    Transmission    r   transmission-gtk)

ex=(Shutdown        wu  systemctl\ poweroff
    Reboot          ru  systemctl\ reboot
    Suspend         sa  systemctl\ suspend
    Lock            l   glock
    Reload\ i3      e   'i3-msg reload && dunstify i3:\ reload'
    Restart\ i3     t   i3-msg\ restart
    Config\ i3      c   gedit\ ~/.config/i3/config)

rt ${(P)1}
